package com.xiaomu.renderer
{
	import com.xiaomu.data.Team;
	import com.xiaomu.manager.DataManager;
	
	import flash.events.MouseEvent;
	
	import coco.component.Alert;
	import coco.component.Button;
	import coco.component.DefaultItemRenderer;
	import coco.component.TextAlign;
	import coco.event.ItemRendererEvent;
	import coco.event.UIEvent;
	
	public class TeamItemRenderer extends DefaultItemRenderer
	{
		public function TeamItemRenderer()
		{
			super();
			mouseChildren = true;
		}
		
		private var editButton:Button;
		private var deleteButton:Button;
		
		override protected function createChildren():void
		{
			super.createChildren();
			
			labelDisplay.leftMargin = 10;
			labelDisplay.textAlign = TextAlign.LEFT;
			
			editButton = new Button();
			editButton.label = "E";
			editButton.toolTip = "编辑";
			editButton.width = editButton.height = 16;
			addChild(editButton);
			
			deleteButton = new Button();
			deleteButton.label = "D";
			deleteButton.width = deleteButton.height = 16;
			deleteButton.toolTip = "删除"; 
			deleteButton.addEventListener(MouseEvent.CLICK, deleteButton_clickHandler);
			addChild(deleteButton);
		}
		
		override protected function updateDisplayList():void
		{
			super.updateDisplayList();
			
			deleteButton.x = width - 10 - deleteButton.width;
			deleteButton.y = (height - deleteButton.height) / 2;
			
			editButton.x = deleteButton.x - 5 - editButton.width;
			editButton.y = (height - editButton.height) / 2;
		}
		
		override protected function this_selectedHandler(event:ItemRendererEvent):void
		{
			event.preventDefault();
		}
		
		protected function deleteButton_clickHandler(event:MouseEvent):void
		{
			Alert.show("确认要删除这个小组吗?", "提示", Alert.CANCEL|Alert.OK, deleteAlertCloseHandler);
		}
		
		private function deleteAlertCloseHandler(e:UIEvent):void
		{
			if (e.detail == Alert.OK)
			{
				DataManager.getInstance().removeTeam(data as Team);
			}
		}
		
	}
}